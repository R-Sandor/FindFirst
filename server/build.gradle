buildscript {
  repositories { mavenCentral() }
  dependencies { classpath("org.flywaydb:flyway-database-postgresql:10.1.0") }
}

plugins {
  id 'application'
  id 'org.springframework.boot' version '3.4.5'
  id 'io.spring.dependency-management' version '1.1.7'
  id "org.sonarqube" version "5.1.0.4882"
  id "com.diffplug.spotless" version "6.19.0"
  id "org.flywaydb.flyway" version "10.1.0"
  id 'java'
  id 'jacoco'
}


group = 'dev.findfirst'
version = '1.0.0'

// Apply a specific Java toolchain to ease working on different environments.
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

spotless {
  // optional: limit format enforcement to just the files changed by this feature branch
  //ratchetFrom 'origin/main'

  format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'
    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
    encoding 'UTF-8'
  }

  java {
    // optional: you can specify import groups directly
    // note: you can use an empty string for all the imports you didn't specify explicitly, and '\\#` prefix for static imports
    importOrder('java', 'jakarta', 'dev.findfirst')
    // optional: instead of specifying import groups directly you can specify a config file
    // export config file: https://github.com/diffplug/spotless/blob/main/ECLIPSE_SCREENSHOTS.md#creating-spotlessimportorder

    removeUnusedImports()

    // Choose one of these formatters.
    eclipse().configFile("eclipse-java-google-style.xml")

    formatAnnotations()  // fixes formatting of type annotations, see below

    //licenseHeader '/* (C) $YEAR */' // or licenseHeaderFile
  }
}

task createKeys(type: JavaExec) {
    main = 'dev.findfirst.security.util.KeyGenerator'  // Replace with the fully qualified class name of your utility
    classpath = sourceSets.main.runtimeClasspath
}

tasks.named("build") {
  dependsOn("spotlessApply")
}

tasks.named("bootRun") {
  dependsOn('flywayMigrate')
}



repositories {
  mavenCentral()
}

dependencies {
  compileOnly 'org.projectlombok:lombok'

  implementation 'org.springframework.boot:spring-boot-starter-data-rest'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-mail'

  implementation 'org.jsoup:jsoup:1.17.2'
  implementation 'org.flywaydb:flyway-core:10.17.1'
  // https://mvnrepository.com/artifact/org.springframework.data/spring-data-jdbc
  implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
  implementation group: 'com.github.crawler-commons', name: 'crawler-commons', version: '1.4'
  implementation 'org.apache.httpcomponents.client5:httpclient5:5.5-alpha1'
  implementation 'org.typesense:typesense-java:1.3.0'

  runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.8.1'
  runtimeOnly 'org.postgresql:postgresql:42.7.5'
  runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
  //implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
  //runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
  implementation 'io.jsonwebtoken:jjwt-api:0.11.5'

  annotationProcessor 'org.projectlombok:lombok'

  testAnnotationProcessor 'org.projectlombok:lombok'

  testCompileOnly 'org.projectlombok:lombok'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.testcontainers:postgresql'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation "org.testcontainers:junit-jupiter"
  testImplementation 'org.junit.jupiter:junit-jupiter-api'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

}

application {
  mainClass = 'dev.findfirst.FindFirstApplication'
}

bootRun
{
  String activeProfile = System.properties['spring.profiles.active']
  if(activeProfile == null || activeProfile.isBlank()) {
    systemProperty "spring.profiles.active", "dev";
  }
}

test {
  systemProperty 'spring.profiles.active', 'integration'
  useJUnitPlatform()
  testLogging.showStandardStreams = true
  finalizedBy jacocoTestReport // report is always generated after tests run
  dependsOn("createKeys")
}

jacocoTestReport {
  reports {
    xml.required = true
    csv.required = true
    xml.destination = file("$buildDir/reports/coverage.xml")
  }
}

sonar {
  properties {
    property "sonar.projectKey", "findfirst_server"
    property "sonar.organization", "findfirst"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.gradle.skipCompile", "true"
  }
}

def setFlywayDev(dbScripts) {
  addDevScriptsToMigrations(dbScripts);
  flyway.cleanDisabled = false;
}

def isDevProfile(String profile) {
 return profile == null || profile.equalsIgnoreCase("integration") || profiles.equalsIgnoreCase("dev");
}

def addDevScriptsToMigrations(dbScripts) {
    dbScripts.add("classpath:db/dev");
    flyway.locations = dbScripts;
}

def setFlywayPrefrences(profiles, dbScripts) {
  if(profiles != null) {
    boolean isList = profiles instanceof List;
    if (isList) {
      for(String prof in profiles) {
        if(isDevProfile(prof)) {
          setFlywayDev(dbScripts);
          break;
        }
      }
    } else if(isDevProfile(profiles)) {
      setFlywayDev(dbScripts);
    }
  }
  // spring.profiles.active are always set for production,
  // otherwise assume we're in development if null.
  else {
    setFlywayDev(dbScripts);
  }
}



flyway {
  // our defaults
  def dbScripts = ["classpath:db/migration"];
  locations = dbScripts;
  cleanDisabled = true;

  // Set env preferences
  String profiles = System.getenv("SPRING_PROFILES_ACTIVE");
  setFlywayPrefrences(profiles, dbScripts);

  // Get the system property or use the default, i.e. development defaults.
  url = System.getProperty("SPRING_DATASOURCE_URL", "jdbc:postgresql://localhost:5432/findfirst")
  user = System.getProperty("POSTGRES_USER", "postgres")
  password = System.getProperty("POSTGRES_PASSWORD", "admin")
  driver = "org.postgresql.Driver"
}


flywayMigrate.dependsOn classes
